plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.parcelize'
}

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion
    ndkVersion rootProject.ndkVersion
    defaultConfig {
        applicationId "io.github.shadowsocksrb"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.sdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        resConfigs 'zh-rCN'
        resValue 'string', 'applicationId', applicationId
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            versionNameSuffix '-nightly'
            signingConfig signingConfigs.debug
            proguardFiles 'r8-rules.pro'
            kotlinOptions {
                freeCompilerArgs = [
                        '-Xno-param-assertions',
                        '-Xno-call-assertions',
                        '-Xno-receiver-assertions',
                        '-Xmultifile-parts-inherit',
                ]
            }
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
            resValue 'string', 'applicationId', defaultConfig.applicationId + applicationIdSuffix
        }
    }
    signingConfigs {
        config {
            enableV3Signing true
            enableV4Signing false
        }
    }
    buildFeatures {
        viewBinding true
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }
    packagingOptions {
        jniLibs {
            excludes += ['/META-INF/**', '/kotlin/**']
        }
        resources {
            excludes += ['/META-INF/**', '/kotlin/**', '/*.bin']
        }
    }
    kotlinOptions.jvmTarget = javaVersion
    dependenciesInfo.includeInApk false
    androidResources {
        additionalParameters '--preferred-density', 'xxxhdpi'
    }
    lint {
        checkReleaseBuilds false
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    implementation project(':core')
    implementation 'androidx.browser:browser:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'com.google.zxing:core:3.4.1'
    implementation 'com.takisoft.preferencex:preferencex-simplemenu:1.1.0'
    implementation 'com.twofortyfouram:android-plugin-api-for-locale:1.0.4'
    implementation 'me.zhanghai.android.fastscroll:library:1.1.7'
}
