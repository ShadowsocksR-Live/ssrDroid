plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.parcelize'
    id 'org.jetbrains.kotlin.kapt'
}

android {
    namespace 'com.github.shadowsocks.core'
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion
    ndkVersion rootProject.ndkVersion
    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.sdkVersion
        buildConfigField("int", "VERSION_CODE", "${rootProject.versionCode}")
        externalNativeBuild {
            ndkBuild {
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
                arguments "-j${Runtime.runtime.availableProcessors()}"
            }
            cmake {
                cFlags '-Wall -Wextra -std=gnu99'
            }
        }
        kapt.arguments {
            arg("room.incremental", true)
            arg("room.schemaLocation", "$projectDir/schemas")
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }

    kotlinOptions.jvmTarget = javaVersion

    externalNativeBuild {
        cmake {
            path file('src/main/jni/CMakeLists.txt')
            version '3.18.1'
        }
    }
}

dependencies {
    api project(':plugin')
    api 'androidx.appcompat:appcompat:1.4.1'
    api 'androidx.fragment:fragment-ktx:1.4.1'
    api "androidx.lifecycle:lifecycle-common-java8:2.4.1"
    api "androidx.lifecycle:lifecycle-livedata-core-ktx:2.4.1"
    api 'androidx.preference:preference-ktx:1.1.1'
    api "androidx.room:room-runtime:2.4.2"
    api "androidx.work:work-runtime-ktx:2.7.1"
    api "androidx.work:work-multiprocess:2.7.1"
    api 'com.google.code.gson:gson:2.9.0'
    api 'dnsjava:dnsjava:3.5.0'
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.1-native-mt"
    api 'org.connectbot.jsocks:jsocks:1.0.0'
    kapt "androidx.room:room-compiler:2.4.2"
}

task deleteCxx(type: Delete) {
    delete file('.cxx')
}

clean.finalizedBy deleteCxx
