plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.parcelize'
    id 'org.jetbrains.kotlin.kapt'
}

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion
    ndkVersion rootProject.ndkVersion
    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.sdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        buildConfigField("int", "VERSION_CODE", "${rootProject.versionCode}")
        externalNativeBuild {
            ndkBuild {
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
                arguments "-j${Runtime.runtime.availableProcessors()}"
            }
        }
        kapt.arguments {
            arg("room.incremental", true)
            arg("room.schemaLocation", "$projectDir/schemas")
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }

    kotlinOptions.jvmTarget = javaVersion

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }
}

def coroutinesVersion = '1.5.1'
def lifecycleVersion = '2.3.1'
def roomVersion = '2.3.0'
def workVersion = '2.7.0-alpha04'
dependencies {
    api project(':plugin')
    api 'androidx.appcompat:appcompat:1.3.0'
    api 'androidx.fragment:fragment-ktx:1.3.5'
    api "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
    api "androidx.lifecycle:lifecycle-livedata-core-ktx:$lifecycleVersion"
    api 'androidx.preference:preference-ktx:1.1.1'
    api "androidx.room:room-runtime:$roomVersion"
    api "androidx.work:work-runtime-ktx:$workVersion"
    api "androidx.work:work-multiprocess:$workVersion"
    api 'com.google.code.gson:gson:2.8.7'
    api 'dnsjava:dnsjava:3.4.0'
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
    api 'org.connectbot.jsocks:jsocks:1.0.0'
    kapt "androidx.room:room-compiler:$roomVersion"
}

task deleteCxx(type: Delete) {
    delete file('.cxx')
}

clean.finalizedBy deleteCxx
